# TODO: This script requires some rework!
cmake_minimum_required(VERSION 3.21)

project(LaTeX2AI)

add_compile_definitions(WIN_ENV=1)
add_compile_definitions(_WINDOWS=1)

# This variable is set in the original solution, however, here this causes the plugin to not be loaded by Illustrator
set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")

# Library to convert data to a base64 string
add_library(base64 STATIC)
target_sources(base64 PRIVATE "tpl/base64/src/base64.cpp")
target_include_directories(
  base64 PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/tpl/base64/src>)

# Minimal XML library
add_library(tinyxml2 STATIC)
target_sources(tinyxml2 PRIVATE "tpl/tinyxml2/tinyxml2.cpp")
target_include_directories(
  tinyxml2 PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/tpl/tinyxml2>)

# Library to get the runtime stack on Windows
add_library(StackWalker STATIC)
target_sources(StackWalker
               PRIVATE "tpl/StackWalker/Main/StackWalker/StackWalker.cpp")
target_include_directories(
  StackWalker
  PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/tpl/StackWalker/Main/StackWalker>
) 

# This library contains all AI interfaces that LaTeX2AI interacts with
add_library(IllustratorSDK STATIC)
target_sources(
  IllustratorSDK
  PRIVATE "../../common/source/SDKAboutPluginsHelper.cpp"
          "../../common/source/Suites.cpp"
          "../../common/source/IllustratorSDK.cpp"
          "../../common/source/FlashUIController.cpp"
          "../../common/source/SDKPlugPlug.cpp"
          "../../common/source/SDKErrors.cpp"
          "../../common/source/AppContext.cpp"
          "../../../illustratorapi/illustrator/AIAssert.cpp"
          "../../../illustratorapi/ate/IThrowException.cpp"
          "../../../illustratorapi/illustrator/IAIFilePath.cpp"
          "../../../illustratorapi/illustrator/IAIUnicodeString.cpp"
          "../../../illustratorapi/illustrator/IAIStringFormatUtils.cpp")
target_include_directories(
  IllustratorSDK
  PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../common/includes>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../../illustratorapi>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../../illustratorapi/pica_sp>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../../illustratorapi/illustrator>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../../illustratorapi/ate>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../../illustratorapi/ate/slotextdomtypes>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../../illustratorapi/ate/legacy>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../../illustratorapi/illustrator/config>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../../illustratorapi/illustrator/config/compiler>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../../illustratorapi/illustrator/actions>
)

# This library contains symbols that habe to be passed to the final LaTeX2AI library so Illustrator can find the plugin
add_library(IllustratorObjects OBJECT)
target_link_libraries(IllustratorObjects PUBLIC IllustratorSDK)
target_sources(IllustratorObjects PRIVATE "../../common/source/Main.cpp"
                                          "../../common/source/Plugin.cpp")
target_include_directories(
  IllustratorObjects
  PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/../../common/win>)

# Add the main LaTeX2AI library
add_library(${PROJECT_NAME} SHARED)

set_target_properties(${PROJECT_NAME} PROPERTIES PREFIX "" SUFFIX ".aip")
set_target_properties(${PROJECT_NAME} PROPERTIES CXX_STANDARD 17)
# This is reqired, see:
# https://community.adobe.com/t5/illustrator-discussions/how-to-build-an-illustrator-c-plugin-with-cmake/td-p/13787731
# https://github.com/Voronwe-the-guide/IllustratorPluginCmake 
set_target_properties(${PROJECT_NAME} PROPERTIES WINDOWS_EXPORT_ALL_SYMBOLS ON)

target_link_libraries(${PROJECT_NAME} PRIVATE IllustratorObjects)
target_link_libraries(${PROJECT_NAME} PRIVATE base64)
target_link_libraries(${PROJECT_NAME} PRIVATE tinyxml2)
target_link_libraries(${PROJECT_NAME} PRIVATE StackWalker)

# Set link to the json library
target_include_directories(
  ${PROJECT_NAME}
  PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/tpl/json/single_include/nlohmann>
)

# Add the actual source and resources
add_subdirectory(src)
add_subdirectory(resources)

# Set the precompiled header
target_precompile_headers(${PROJECT_NAME} PRIVATE
                          ../../common/includes/IllustratorSDK.h)

# TODO:
# We need to generate the autogenerated headers